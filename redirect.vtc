varnishtest "Test redirect"
# Redirect incoming requests
#

# Backend that just serves 503
server s1 {
	rxreq
	txresp -status 200
	expect req.url == "/"
} -start

varnish v1 -vcl+backend {
	vcl 4.0;

	sub vcl_recv {
		if (req.url ~ "/permanent") {
			return(synth(301, "https://new.example.com" + req.url));
		}

		if (req.url ~ "/temporary") {
			return(synth(302, "https://temporary.example.com" + req.url));
		}
	}

	sub vcl_synth {
		# Permanent redirect
		if (resp.status == 301) {
			set resp.http.Location = resp.reason;
			set resp.reason = "Moved Permanently";
			return (deliver);
		}

		# Temporary redirect
		if (resp.status == 302) {
			set resp.http.Location = resp.reason;
			set resp.reason = "Found";
			return (deliver);
		}

	}
} -start

client c1 {
	txreq -url "/permanent" -hdr "host: example.com"
	rxresp
	expect resp.status == 301
	expect resp.http.location == "https://new.example.com/permanent"
} -run

client c2 {
	txreq -url "/temporary" -hdr "host: example.com"
	rxresp
	expect resp.status == 302
	expect resp.http.location == "https://temporary.example.com/temporary"
} -run
